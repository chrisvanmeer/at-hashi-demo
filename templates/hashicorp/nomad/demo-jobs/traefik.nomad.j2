job "traefik" {
  datacenters = ["{{ hashicorp_datacenter_name }}"]
  type        = "service"

  // For demo purposes, keep this job on the first client.
  constraint {
    attribute = "${node.unique.id}"
    operator  = "="
    value     = "{{ nomad_first_client_node_id.stdout }}"
  }

  group "traefik" {
    count = 1

    network {
      port "http" {
        static = 80
      }

      port "https" {
        static = 443
      }

      port "api" {
        static = 8081
      }

      port "health" {
        static = 8082
      }
    }

    service {
      name = "traefik"

      check {
        type     = "http"
        path     = "/ping"
        port     = "health"
        interval = "10s"
        timeout  = "2s"
      }
    }

    task "traefik" {

      driver = "docker"
      config {
        image        = "{{ traefik_demo_docker_image }}"
        network_mode = "host"

        volumes = [
          "local/traefik.toml:/etc/traefik/traefik.toml",
          "local/ssl:/etc/traefik/ssl",
        ]
      }
      vault {
        policies      = ["ssl-certificates-policy"]
        change_mode   = "signal"
        change_signal = "SIGHUP"
      }

      template {
        data = <<-EOH
        [entryPoints]
            [entryPoints.http]
            address = ":80"
            [entryPoints.http.http.redirections]
              [entryPoints.http.http.redirections.entryPoint]
                to = "https"
                scheme = "https"
                permanent = true
            [entryPoints.https]
            address = ":443"
            [entryPoints.https.http]
              middlewares = ["hsts@file"]
              [entryPoints.https.http.tls]
            [entryPoints.api]
            address = ":8081"
            [entryPoints.ping]
            address = ":8082"

        [tls.options]
          [tls.options.default]
            sniStrict = true
            minVersion = "VersionTLS12"

        [api]
            dashboard = true
            insecure  = true
        [pilot]
            dashboard = false

        [providers]
          [providers.file]
            directory = "/local/rules"
            watch = true
          [providers.consulCatalog]
              prefix           = "traefik"
              exposedByDefault = false

              [providers.consulCatalog.endpoint]
                address = "127.0.0.1:8500"
                scheme  = "http"

        [ping]
          entryPoint = "ping"

        [log]
          format = "json"

        [accessLog]
          format = "json"

        EOH
        destination = "local/traefik.toml"
      }

      template {
        destination = "local/rules/sts.yml"
        data = <<-EOH
        http:
          middlewares:
            hsts:
              headers:
                stsSeconds: 63072000
                stsIncludeSubdomains: true
                stsPreload: true
        EOH
      }

      template {
        destination = "local/rules/tls.yml"
        data = <<-EOH
        tls:
          certificates:
            - certFile: /etc/traefik/ssl/tls.crt
              keyFile: /etc/traefik/ssl/tls.key
        EOH
      }

      template {
        destination = "local/ssl/tls.key"
        left_delimiter = "{!"
        right_delimiter = "!}"
        data = <<-EOH
        {! with secret "secret/data/ssl-certificates/webapp" !}{! .Data.data.privatekey !}{! end !}
        EOH
      }

      template {
        destination = "local/ssl/tls.crt"
        left_delimiter = "{!"
        right_delimiter = "!}"
        data = <<-EOH
        {! with secret "secret/data/ssl-certificates/webapp" !}{! .Data.data.certificate !}{! end !}
        EOH
      }

      resources {
        cpu    = 100
        memory = 128
      }

    }

  }

}
